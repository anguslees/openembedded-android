DESCRIPTION = "Qt interface for choosing which configured network to connect \
to. It also provides a method for browsing 802.11 SSID scan results, an event \
history log of messages generated by wpa_supplicant, and a method to add or \
edit wpa_supplicant networks."
SECTION = "network"
LICENSE = "GPL BSD"
HOMEPAGE = "http://hostap.epitest.fi/wpa_supplicant/"
DEPENDS = "librsvg-native imagemagick-native"
RDEPENDS_${PN} = "wpa-supplicant"
RRECOMMENDS_${PN} = "${LIBC}-gconv-utf-16"
PR = "r2"

SRC_URI = "http://hostap.epitest.fi/releases/wpa_supplicant-${PV}.tar.gz;name=archive\
         file://icons.patch \
         file://session_manager.patch "

SRC_URI_append_shr = "file://desktop.patch \
	              file://style"

S = "${WORKDIR}/wpa_supplicant-${PV}/wpa_supplicant/wpa_gui-qt4"

inherit qt4x11
ARM_INSTRUCTION_SET = "arm"

do_compile_append() {
       oe_runmake -C ${S}/icons
}

do_install () {
       install -d ${D}${bindir}
       install -d ${D}${datadir}/wpa_gui
       install -d ${D}${datadir}/applications
       install -d ${D}${datadir}/pixmaps
       install -m 644 icons/hicolor/128x128/apps/wpa_gui.png ${D}${datadir}/pixmaps
       install -m 755 wpa_gui ${D}/${bindir}
       install -m 644 wpa_gui.desktop ${D}/${datadir}/applications
}

do_install_append_shr() {
       install -m 644 ${WORKDIR}/style ${D}/${datadir}/wpa_gui
}

FILES_${PN} = "/usr/share/pixmaps/wpa_gui.png \
               /usr/share/wpa_gui/style \
               /usr/bin/wpa_gui \
               /usr/share/applications/wpa_gui.desktop"

FILES_${PN}_append_shr = " /usr/share/wpa_gui/style"

SRC_URI[archive.md5sum] = "0efb8fcedf0a8acf6f423dfdb0658fdd"
SRC_URI[archive.sha256sum] = "6e5284fa3f7c56e001fae92486eaa31340db5dd925dcb38bd572d2e8ed70f570"
